(defpackage cl-training.parsers
  (:use :cl :maxpc :maxpc.digit :maxpc.char :parse-number :cl-training.classes)
  (:export :=trainings))
(in-package :cl-training.parsers)

(defun =int ()
  (=integer-number 10))

(defun =date ()
  (=destructure (y _ m _ d)
				(=list (=int) (?eq #\/) (=int) (?eq #\/) (=int))
	(list y m d)))

(defun =float ()
  (=transform
   (=subseq
	(?seq (%some (?digit 10))
		  (%maybe (?seq (?eq #\.)
						(%any (?digit 10))))))
   #'parse-number:parse-number))

(defun =comma-separated (fn)
  (=destructure (f l)
				(=list (funcall fn)
					   (%any (=destructure (_ n) (=list (?eq #\,)
														(funcall fn))
							   n)))
	(cons f l)))

(defun =comma-separated-ints ()
  (=comma-separated #'=int))

(defun =comma-separated-floats ()
  (=comma-separated #'=float))

(defun =set-only-reps ()
  (=transform (=comma-separated-ints)
			  #'(lambda (sets) (list :reps sets))))

(defun =set-sets-reps-weights ()
  (=destructure (sets rw)
				(=list (=transform (=list (=comma-separated-ints)
										  (?eq #\*))
								   #'first)
					   (=transform (=set-reps-weights)
								   #'rest))
	(cons :sets-reps-weights (cons sets rw))))

(defun =set-reps-weights ()
  (=destructure (sets reps)
				(=list (=comma-separated-ints)
					   (=destructure (_ reps)
									 (=list (?eq #\*)
											(=comma-separated-floats))
						 reps))
	(list :reps-weights sets reps)))

(defun =set ()
  (%or (=set-sets-reps-weights)
	   (=set-reps-weights)
	   (=set-only-reps)))

(defun =word ()
  (=subseq (%some (%or (?satisfies 'alpha-char-p)
					   (?satisfies (lambda (c)
									 (member c '(#\- #\_ #\( #\)))))))))

(defun =exercise-name ()
  (=subseq (?seq (=word)
				 (%any (?seq (%maybe (%some (?whitespace)))
							 (=word))))))

(defun =exercise ()
  (=destructure (exercise _ reps)
				(=list (=exercise-name)
					   (%some (?whitespace))
					   (%some (=transform  (=list (=set)
												  (%maybe (?eq #\space)))
										   #'first)))
	(make-exercise exercise reps)))

(defun =training ()
  (=destructure (d _ e)
				(=list (=date)
					   (?newline)
					   (%some (=destructure (tr _)
											(=list (=exercise)
												   (%maybe (?newline)))
								tr)))
	(make-training d e)))

(defun =trainings ()
  (=list (%some  (=destructure (tr _)
							   (=list (=training)
									  (%any (?newline)))
				   tr))))